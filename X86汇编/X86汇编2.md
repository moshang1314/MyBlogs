# 1 8086的无条件转移指令 jmp指令

## 1.1 相对短转移和16位相对近转移（段内转移）

偏移量 = jmp后一条指令地址 - 标号

IP = IP + 偏移量

```assembly
jmp short 标号/目标处的汇编地址			;机器码：EB 1字节偏移量  在编译过程中被计算
jmp near 标号/目标处的汇编地址			;机器码：E9 2字节偏移量  在编译过程中被计算
jmp 标号/目标处的汇编地址					;near/short可由编译器来决定
```

## 1.2 16位间接近转移（段内转移）

取出寄存器或内存地址中的16位段内偏移地址赋予IP

```assembly
jmp r/m

jmp cx
jmp [0x3000]
jmp [bx]
```

## 1.3 16位直接绝对远转移（段间转移）

**jmp	16位段地址：16位偏移地址，		CS = 16位段地址，IP=16位偏移地址**

```assembly
jmp	0x052e:0x003c
```

## 1.4 16位间接绝对远转移(段间转移)

**jmp far m			内存地址m处，保存了目标位置的段基址和段内偏移地址，低16位为段内偏移地址，高16位为段基址。**

```assembly
jmp far [0x2002]
jmp far [bx]
```

# 2 resb、resw和resd指令

resb(reserved bytes)：在编译过程中以字节为单位保留空间，不对其进行初始化，所以原本内存中是什么数据就是什么数据

resw(reserved words)：在编译过程中以字为单位保留空间，不对其进行初始化，所以原本内存中是什么数据就是什么数据

resd(reserved double words)：在编译过程中以双字为单位保留空间，不对其进行初始化，所以原本内存中是什么数据就是什么数据

```assembly
resb 256
;等价于
resw 128
;等价于
resd 64
```

# 3 用户程序的执行过程

![image-20220710191618929](G:\images\Linux\image-20220710191618929.png)

```assembly
				;包含代码段、数据段和栈段的用户程序
;=====================================================================================
SECTION header vstart=0		;用户程序头部段
	program_length dd program_end	;程序总长度[0x00]
	
	;用户程序入口点
	code_entry		dw start	;偏移地址[0x04]
					dd section.code.start	;段地址[0x06]
	
	realloc_tbl_len	dw (segtbl_end-segtbl_begin)/4		;段重定位表项个数[0x0A]
	
	;段重定位表
	segtbl_begin:
	code_segment	dd section.code.start		;[0x0C]
	data_segment	dd section.data.start		;[0x10]
	stack_segment	dd section.stack.start		;[0x14]
	segtbl_end:
		
;============================================================================================
SECTION code align=16 vstart=0								;代码段
	start:
		;初始执行时，DS和ES指向用户程序头部段
		mov ax, [stack_segment]								;设置到用户程序自己的堆栈
		mov ss, ax											
		mov sp, stack_pointer								;设置初始的栈顶指针
		
		mov ax, [data_segment]								;设置到用户程序自己的数据段
		mov ds, ax
		
		mov ax, 0xb800
		mov es, ax
		
		mov si, message
		mov di, 0
		
	next:
		mov al, [si]
		cmp al, 0
		je exit
		mov byte [es:di], al
		mov byte [es:di+1], 0x07
		inc si
		add di, 2
		jmp next
		
	exit:
		jmp $

;=============================================================================================
SECTION data align=16 vstart=0								;数据段
	message			db 'hello world.', 0
	
;=============================================================================================
SECTION stack align=16 vstart=0								;栈段
					resb 256
	stack_pointer:

;=============================================================================================
SECTION trail align=16										;尾部
program_end:
```

# 4 与文本显示有关的回车、换行和光标控制

![image-20220711183356592](G:\images\Linux\image-20220711183356592.png)

显卡内部拥有许多寄存器，并进行了编号，为了不消耗端口编码，显卡接口只占用两个8位端口，索引端口0x3D4和数据端口0x3D5，索引端口负责索引寄存器号，数据端口负责连通寄存器传送数据。在显卡接口的寄存器中有两个寄存器维护着光标的位置，它们分别是0x0e和0x0f。

![image-20220712100949446](G:\images\Linux\image-20220712100949446.png)    

```assembly
         ;代码清单8-2
         ;文件名：c08.asm
         ;文件说明：用户程序 
         ;创建日期：2011-5-5 18:17
         
;===============================================================================
SECTION header vstart=0                     ;定义用户程序头部段 
    program_length  dd program_end          ;程序总长度[0x00]
    
    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code_1.start ;段地址[0x06] 
    
    realloc_tbl_len dw (header_end-code_1_segment)/4
                                            ;段重定位表项个数[0x0a]
    
    ;段重定位表           
    code_1_segment  dd section.code_1.start ;[0x0c]
    code_2_segment  dd section.code_2.start ;[0x10]
    data_1_segment  dd section.data_1.start ;[0x14]
    data_2_segment  dd section.data_2.start ;[0x18]
    stack_segment   dd section.stack.start  ;[0x1c]
    
    header_end:                
    
;===============================================================================
SECTION code_1 align=16 vstart=0         ;定义代码段1（16字节对齐） 
put_string:                              ;显示串(0结尾)。
                                         ;输入：DS:BX=串地址
         mov cl,[bx]
         or cl,cl                        ;cl=0 ?
         jz .exit                        ;是的，返回主程序 
         call put_char
         inc bx                          ;下一个字符 
         jmp put_string

   .exit:
         ret

;-------------------------------------------------------------------------------
put_char:                                ;显示一个字符
                                         ;输入：cl=字符ascii
         push ax
         push bx
         push cx
         push dx
         push ds
         push es

         ;以下取当前光标位置
         mov dx,0x3d4
         mov al,0x0e
         out dx,al						;把寄存器索引号发送给显卡索引端口
         mov dx,0x3d5					;从数据端口获取数据
         in al,dx                        ;高8位 
         mov ah,al

         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         in al,dx                        ;低8位 
         mov bx,ax                       ;BX=代表光标位置的16位数

         cmp cl,0x0d                     ;回车符？
         jnz .put_0a                     ;不是。看看是不是换行等字符 
         mov ax,bx                       ;此句略显多余，但去掉后还得改书，麻烦 
         mov bl,80                       
         div bl
         mul bl
         mov bx,ax
         jmp .set_cursor

 .put_0a:
         cmp cl,0x0a                     ;换行符？
         jnz .put_other                  ;不是，那就正常显示字符 
         add bx,80
         jmp .roll_screen

 .put_other:                             ;正常显示字符
         mov ax,0xb800
         mov es,ax
         shl bx,1
         mov [es:bx],cl

         ;以下将光标位置推进一个字符
         shr bx,1
         add bx,1

 .roll_screen:
         cmp bx,2000                     ;光标超出屏幕？滚屏
         jl .set_cursor

		 push bx
         mov ax,0xb800
         mov ds,ax
         mov es,ax
         cld
         mov si,0xa0
         mov di,0x00
         mov cx,1920
         rep movsw
         mov bx,3840                     ;清除屏幕最底一行
         mov cx,80
 .cls:
         mov word[es:bx],0x0720
         add bx,2
         loop .cls

         ;mov bx,1920
		 pop bx
		 sub bx, 80

 .set_cursor:
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         mov dx,0x3d5
         mov al,bh
         out dx,al
         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         mov al,bl
         out dx,al

         pop es
         pop ds
         pop dx
         pop cx
         pop bx
         pop ax

         ret

;-------------------------------------------------------------------------------
  start:
         ;初始执行时，DS和ES指向用户程序头部段
         mov ax,[stack_segment]           ;设置到用户程序自己的堆栈 
         mov ss,ax
         mov sp,stack_end
         
         mov ax,[data_1_segment]          ;设置到用户程序自己的数据段
         mov ds,ax

         mov bx,msg0
         call put_string                  ;显示第一段信息 

         push word [es:code_2_segment]
         mov ax,begin
         push ax                          ;可以直接push begin,80386+
         
         retf                             ;转移到代码段2执行 
         
  continue:
         mov ax,[es:data_2_segment]       ;段寄存器DS切换到数据段2 
         mov ds,ax
         
         mov bx,msg1
         call put_string                  ;显示第二段信息 

         jmp $ 

;===============================================================================
SECTION code_2 align=16 vstart=0          ;定义代码段2（16字节对齐）

  begin:
         push word [es:code_1_segment]
         mov ax,continue
         push ax                          ;可以直接push continue,80386+
         
         retf                             ;转移到代码段1接着执行 
         
;===============================================================================
SECTION data_1 align=16 vstart=0

    msg0 db '  This is NASM - the famous Netwide Assembler. '
         db 'Back at SourceForge and in intensive development! '
         db 'Get the current versions from http://www.nasm.us/.'
         db 0x0d,0x0a,0x0d,0x0a
         db '  Example code for calculate 1+2+...+1000:',0x0d,0x0a,0x0d,0x0a
         db '     xor dx,dx',0x0d,0x0a
         db '     xor ax,ax',0x0d,0x0a
         db '     xor cx,cx',0x0d,0x0a
         db '  @@:',0x0d,0x0a
         db '     inc cx',0x0d,0x0a
         db '     add ax,cx',0x0d,0x0a
         db '     adc dx,0',0x0d,0x0a
         db '     inc cx',0x0d,0x0a
         db '     cmp cx,1000',0x0d,0x0a
         db '     jle @@',0x0d,0x0a
         db '     ... ...(Some other codes)',0x0d,0x0a,0x0d,0x0a
         db 0

;===============================================================================
SECTION data_2 align=16 vstart=0

    msg1 db '  The above contents is written by LeeChung. '
         db '2011-05-06'
         db 0

;===============================================================================
SECTION stack align=16 vstart=0
           
         resb 256

stack_end:  

;===============================================================================
SECTION trail align=16
program_end:
```

​                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              

# 5 有符号数乘法指令mul和无符号数乘法指令imul

<center><h1> mul r/m </h1></center>

## 5.1 16位乘法

如果在指令中指定的是8位寄存器或者8位操作数的内存地址，**则意味着被乘数是8位的，在寄存器AL里**。

**相乘后，乘积是16位的，在寄存器AX里**。

```assembly
mul bh
mul byte [0x2002]
```

## 5.2 32位乘法

如果在指令中指定的是16位寄存器或者16位操作数的内存地址，则意味着被乘数是16位的，在寄存器AX里。

相乘后，乘积是32位的，低16位在寄存器AX里，高16位在寄存器DX里。

```assembly
mul bx
mul word [0x2002]
```

## 5.3 64位乘法指令（8086不支持，从80386开始支持）

如果在指令中指定的是32位寄存器或者32位操作数的内存地址，则意味着被乘数是32位的，在寄存器EAX里。

相乘后，乘积是64位的，低32位在寄存器EAX里，高32位在寄存器EDX里。



```assembly
mul ebx			;eax X ebx ==> edx:eax
mul dword [0x2002]
```

## 5.4 128位乘法（8086和32位处理器都不支持，只有64位处理器支持）

如果在指令中指定的是64位寄存器或者64位操作数的内存地址，则意味着被乘数是64位的，在寄存器RAX里。

相乘后，乘积是128位的，低64位在寄存器RAX里，高64位在寄存器RDX里。

```assembly
mul rbx
mul qword [0x2002]
```

# 6 中断

中断就是打断处理器当前的执行流程，去执行另外一些和当前工作不相干的指令，执行完之后还可以返回到原来的程序流程继续执行。这就好比是你正在用手机听歌，突然来电话了。处理器必须中断歌曲的播放，来处理这件更为重要的事件。

## 6.1 外部硬件中断

外部硬件中断就是从处理器外面来的中断信号。外部中断是通过两个信号引入处理器内部的。从很早的时候起，也就是8086处理器时代，这两根线的名字就叫NMI和INTR。

![image-20220713164712995](G:\images\Linux\image-20220713164712995.png)

## 6.2 非屏蔽中断和可屏蔽中断

来自外部设备的中断有很多，并不是每一个中断都是必须处理的。有些中断，在任何时候都必须及时处理，因为事关整个系统的安全性。所有的

严重事件都必须无条件地加以处理，这种类型的中断是不会被阻断和屏蔽的，称为非屏蔽中断（Non Maskable Interrupt, NMI）。

可屏蔽中断是通过INTR引脚进入处理器内部的。

## 6.3 中断控制器和8259A芯片

![image-20220713181135709](G:\images\Linux\image-20220713181135709.png)

![image-20220713181950617](G:\images\Linux\image-20220713181950617.png)

![image-20220713182332754](G:\images\Linux\image-20220713182332754.png)

## 6.3 中断处理标志位（Interrupt Flag，IF）

![image-20220713183337602](G:\images\Linux\image-20220713183337602.png)

当IF为0时，来自INTR的的中断信号都被忽略掉。当IF为1时，处理器可以接受和响应可屏蔽中断。

* cli指令：清除标志位IF
* sti指令：设置标志位IF

## 6.4 中断编号和中断处理过程

![image-20220713190527247](G:\images\Linux\image-20220713190527247.png)

### 6.4.1 实模式下的中断向量表

处理器可以识别256个中断。中断向量表存储着每个中断的中断处理程序的段基址和段内偏移地址。中断向量表有bios程序开机初始化时创建的，并存储在0x00000的内存地址中。

![image-20220713192455698](G:\images\Linux\image-20220713192455698.png)

### 6.4.2 中断处理过程

1 **保护断点的现场**

2 **执行中断处理程序**

3 **在遇到iret指令时，返回到断点接着执行**

# 7 实时时钟 CMOSRAM和BCD编码

![image-20220713193803199](G:\images\Linux\image-20220713193803199.png)

## 7.1 二进制形式的十进制编码（BCD）

**CMOSRAM中的时钟信息采用BCD编码。**

**如25用单字节存储为 0010 0101**

## 7.2 RTC电路可以发出的中断信号

* 周期性中断信号（Periodic Interrupt：PF）最慢500ms发生一次，最快34.517us发生一次，可调
* 更新周期结束中断（Update-ended Interrupt：UI）
* 闹钟中断（Alarm Interrupt：AI）

![image-20220713195002244](G:\images\Linux\image-20220713195002244.png)

![image-20220713195425293](G:\images\Linux\image-20220713195425293.png)

![image-20220713195731217](G:\images\Linux\image-20220713195731217.png)

![image-20220713200503241](G:\images\Linux\image-20220713200503241.png)

![image-20220713200810598](G:\images\Linux\image-20220713200810598.png)

![image-20220713201140909](G:\images\Linux\image-20220713201140909.png)

![image-20220714140022476](G:\images\Linux\image-20220714140022476.png)

![image-20220716164151134](G:\images\Linux\image-20220716164151134.png)

```assembly
         ;代码清单9-1
         ;文件名：c09_1.asm
         ;文件说明：用户程序 
         ;创建日期：2011-4-16 22:03
         
;===============================================================================
SECTION header vstart=0                     ;定义用户程序头部段 
    program_length  dd program_end          ;程序总长度[0x00]
    
    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code.start   ;段地址[0x06] 
    
    realloc_tbl_len dw (header_end-realloc_begin)/4
                                            ;段重定位表项个数[0x0a]
    
    realloc_begin:
    ;段重定位表           
    code_segment    dd section.code.start   ;[0x0c]
    data_segment    dd section.data.start   ;[0x14]
    stack_segment   dd section.stack.start  ;[0x1c]
    
header_end:                
    
;===============================================================================
SECTION code align=16 vstart=0           ;定义代码段（16字节对齐） 
new_int_0x70:
      push ax
      push bx
      push cx
      push dx
      push es
      
  .w0:                                    
      mov al,0x0a                        ;阻断NMI。当然，通常是不必要的
      or al,0x80                          
      out 0x70,al
      in al,0x71                         ;读寄存器A
      test al,0x80                       ;测试第7位UIP 
      jnz .w0                            ;以上代码对于更新周期结束中断来说 
                                         ;是不必要的 
      xor al,al
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(秒)
      push ax

      mov al,2
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(分)
      push ax

      mov al,4
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(时)
      push ax

      mov al,0x0c                        ;寄存器C的索引。且开放NMI 
      out 0x70,al
      in al,0x71                         ;读一下RTC的寄存器C，否则只发生一次中断
                                         ;此处不考虑闹钟和周期性中断的情况 
      mov ax,0xb800
      mov es,ax

      pop ax
      call bcd_to_ascii
      mov bx,12*160 + 36*2               ;从屏幕上的12行36列开始显示

      mov [es:bx],ah
      mov [es:bx+2],al                   ;显示两位小时数字

      mov al,':'
      mov [es:bx+4],al                   ;显示分隔符':'
      not byte [es:bx+5]                 ;反转显示属性 

      pop ax
      call bcd_to_ascii
      mov [es:bx+6],ah
      mov [es:bx+8],al                   ;显示两位分钟数字

      mov al,':'
      mov [es:bx+10],al                  ;显示分隔符':'
      not byte [es:bx+11]                ;反转显示属性

      pop ax
      call bcd_to_ascii
      mov [es:bx+12],ah
      mov [es:bx+14],al                  ;显示两位小时数字
      
      mov al,0x20                        ;中断结束命令EOI 
      out 0xa0,al                        ;向从片发送 
      out 0x20,al                        ;向主片发送 

      pop es
      pop dx
      pop cx
      pop bx
      pop ax

      iret

;-------------------------------------------------------------------------------
bcd_to_ascii:                            ;BCD码转ASCII
                                         ;输入：AL=bcd码
                                         ;输出：AX=ascii
      mov ah,al                          ;分拆成两个数字 
      and al,0x0f                        ;仅保留低4位 
      add al,0x30                        ;转换成ASCII 

      shr ah,4                           ;逻辑右移4位 
      and ah,0x0f                        
      add ah,0x30

      ret

;-------------------------------------------------------------------------------
start:
      mov ax,[stack_segment]
      mov ss,ax			;此指令之后到设置栈顶指针之前是禁止中断的
      mov sp,ss_pointer
      mov ax,[data_segment]
      mov ds,ax
      
      mov bx,init_msg                    ;显示初始信息 
      call put_string

      mov bx,inst_msg                    ;显示安装信息 
      call put_string
      
      mov al,0x70
      mov bl,4
      mul bl                             ;计算0x70号中断在IVT（中断向量表）中的偏移
      mov bx,ax                          

      cli                                ;防止改动期间发生新的0x70号中断

      push es
      mov ax,0x0000
      mov es,ax
      mov word [es:bx],new_int_0x70      ;偏移地址。
                                          
      mov word [es:bx+2],cs              ;段地址
      pop es

      mov al,0x0b                        ;RTC寄存器B
      or al,0x80                         ;阻断NMI,最高位为1阻断非可屏蔽中断 
      out 0x70,al
      mov al,0x12                        ;设置寄存器B（0001 0010），禁止周期性中断，开放更 
      out 0x71,al                        ;新结束后中断，BCD码，24小时制 

      mov al,0x0c
      out 0x70,al
      in al,0x71                         ;读RTC寄存器C，复位未决的中断状态

      in al,0xa1                         ;读8259从片的IMR寄存器 
      and al,0xfe                        ;清除bit 0(此位连接RTC)
      out 0xa1,al                        ;写回此寄存器 

      sti                                ;重新开放中断 

      mov bx,done_msg                    ;显示安装完成信息 
      call put_string

      mov bx,tips_msg                    ;显示提示信息
      call put_string
      
      mov cx,0xb800
      mov ds,cx
      mov byte [12*160 + 33*2],'@'       ;屏幕第12行，33列
       
 .idle:
      hlt                                ;使CPU进入低功耗状态，直到用中断唤醒
      not byte [12*160 + 33*2+1]         ;反转显示属性 
      jmp .idle

;-------------------------------------------------------------------------------
put_string:                              ;显示串(0结尾)。
                                         ;输入：DS:BX=串地址
         mov cl,[bx]
         or cl,cl                        ;cl=0 ?
         jz .exit                        ;是的，返回主程序 
         call put_char
         inc bx                          ;下一个字符 
         jmp put_string

   .exit:
         ret

;-------------------------------------------------------------------------------
put_char:                                ;显示一个字符
                                         ;输入：cl=字符ascii
         push ax
         push bx
         push cx
         push dx
         push ds
         push es

         ;以下取当前光标位置
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         mov dx,0x3d5
         in al,dx                        ;高8位 
         mov ah,al

         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         in al,dx                        ;低8位 
         mov bx,ax                       ;BX=代表光标位置的16位数

         cmp cl,0x0d                     ;回车符？
         jnz .put_0a                     ;不是。看看是不是换行等字符 
         mov ax,bx                       ; 
         mov bl,80                       
         div bl
         mul bl
         mov bx,ax
         jmp .set_cursor

 .put_0a:
         cmp cl,0x0a                     ;换行符？
         jnz .put_other                  ;不是，那就正常显示字符 
         add bx,80
         jmp .roll_screen

 .put_other:                             ;正常显示字符
         mov ax,0xb800
         mov es,ax
         shl bx,1
         mov [es:bx],cl

         ;以下将光标位置推进一个字符
         shr bx,1
         add bx,1

 .roll_screen:
         cmp bx,2000                     ;光标超出屏幕？滚屏
         jl .set_cursor

         mov ax,0xb800
         mov ds,ax
         mov es,ax
         cld
         mov si,0xa0
         mov di,0x00
         mov cx,1920
         rep movsw
         mov bx,3840                     ;清除屏幕最底一行
         mov cx,80
 .cls:
         mov word[es:bx],0x0720
         add bx,2
         loop .cls

         mov bx,1920

 .set_cursor:
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         mov dx,0x3d5
         mov al,bh
         out dx,al
         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         mov al,bl
         out dx,al

         pop es
         pop ds
         pop dx
         pop cx
         pop bx
         pop ax

         ret

;===============================================================================
SECTION data align=16 vstart=0

    init_msg       db 'Starting...',0x0d,0x0a,0
                   
    inst_msg       db 'Installing a new interrupt 70H...',0
    
    done_msg       db 'Done.',0x0d,0x0a,0

    tips_msg       db 'Clock is now working.',0
                   
;===============================================================================
SECTION stack align=16 vstart=0
           
                 resb 256
ss_pointer:
 
;===============================================================================
SECTION program_trail
program_end:
```

# 8 test指令 

可以用这条指令来测试某个寄存器，或者内存单元里的内容是否带有某个特征。test指令在功能上和and指令是一样的，都是将两个操作数进行逻辑“与”，并根据

结果设置相应的标志位。但是test指令执行后，运算结果被丢弃（不改变或破坏两个操作数的内容）。

test指令需要两个操作数，其指令格式为：

```assembly
test r/m8, imm8
test r/m16, imm16
test r/m8, r8
test r/m16, r16
```

和and指令一样，test指令执行后，OF=CF=0；对ZF、SF和PF的影响视测试结果而定；对AF的影响未定义。

对于test指令的应用：比如，我们想测试AL寄存器的第3位是“0”还是“1”，可以这样编写代码：

```assembly
test al, 0x08
```

0x08的二进制形式为00001000，它的第3位是“1”，表明我们关注的是这一位。不管寄存器AL中的内容是什么，只要第三位是“0”，这条指令执行后，结果一定是00000000，标志位ZF=1；相反，如果寄存器AL的第3位是“1”，那么结果一定是00001000，ZF=0.于是，根据ZF标志位的情况，就可以判定寄存器AL中的第3位是“0”还是“1”。

# 9 hlt指令

hlt指令使处理器停止执行指令，并处于停机状态，这将降低处理器的功耗。处于停机状态的处理器可以被外部中断唤醒并恢复执行，而且会继续执行hlt后面的指令。

```assembly
.idle:
	hlt			;使CPU进入低功耗状态，直到用中断唤醒
	not byte [12*160 + 33*2+1]		;反转显示属性
	jmp .idle
```

# 10 not指令(按位取反指令)

```assembly
not r/m8
not r/m16
```

not指令执行时，会将操作数的每一位反转，原来的0变成1，原来的1变成0。该指令不影响任何标志位。比如：

```assembly
mov al, 0x1f
not al			;执行后，AL的内容为0xe0
```

# 11 iret指令

iret指令用于恢复中断前标志寄存器、代码段寄存器CS和IP寄存器的内容，保证中断前的执行环境。

![image-20220716164626766](G:\images\Linux\image-20220716164626766.png)

# 12 内部中断

和硬件中断不同，内部中断发生在处理器内部，是由执行的指令引起的。比如，当处理器检测到div或者idiv指令的除数为零时，或者除法的结果溢出时，将产生中断0（0号中断），这就是除法错中断。再比如，当处理器遇到非法指令时，将产生中断6。非法指令是指指令的操作码没有定义，或者指令超过了规定的长度。操作码没有定义通常意味着那不是一条指令，而是普通的数。

内部中断不受标志寄存器IF位的影响，也不需要中断识别总线周期，它们的中断类型是固定的，可以立即转入相应的处理过程。

# 13 软中断

![image-20220718112127211](G:\images\Linux\image-20220718112127211.png)

软中断是由int指令引起的中断处理。这类中断也不需要中断识别总线周期，且不受标志寄存器的IF位影响，中断号在指令中给出。int指令的格式如下：

```assembly
int3
int imm8
into
```

int3是断点中断指令，机器指令码为CC。这条指令在调试程序的时候很有用，当程序运行不正常时，多数时候希望在某个地方设置一个检查点，也称为断点，来查看寄存器、内存单元或者标志寄存器的内容，这条指令就是为这个目的而设的。

指令都是连续存放的，因此，所谓的断点，就是某条指令的起始地址。int3是单字节指令，这是有意设计的。当需要设置断点时，可以将断点处那条指令的第1字节改成0xcc，原字节予以保存。当处理器执行到int3时，即发送3号中断，转去执行相应的的中断处理程序。中断处理程序的执行也要用到各个寄存器，这会破坏它们的内容，但push不会。我们可以在该程序内先压栈所有相关寄存器和内存单元，然后分别取出予以显示，它们就是中断前的现场内容。最后再恢复那条指令的第1字节，并修改位于栈中的返回地址，执行iret指令。

【注意】int3和int 3不是一回事。前者的机器码为CC，后者则是CD 03，这就是通常所说的int n，其操作码为0xCD，第2字节的操作数给出中断号。举几个例子：

```assembly
int 0x00		;引发0号中断
int 0x15		;引发0x15号中断
int 0x16		;引发0x16号中断
```

into 溢出中断指令，机器码为0xCE，也是单字节指令。当处理器执行这条指令时，如果标志寄存器的OF位是1，那么，将产生4号中断。否则，这条指令什么也不做。

# 14 BIOS中断

![image-20220718112330153](G:\images\Linux\image-20220718112330153.png)

可以为所有的中断类型自定义中断处理过程，包括内部中断、硬件中断和软中断。特别是考虑到**处理器允许256种中断类型**，而且大部分都没有被硬件和处理器内部中断占用。

编写自己的中断处理程序有相当大的优越之处。不像jmp和call指令，int指令不需要知道目标程序的入口地址。远转移指令jmp和远调用指令必须直接或者间接给出目标位置的段地址和偏移地址，如果所有一切都是自己安排的，倒也不成问题，但如果想调用别人的代码，比如操作系统的功能，这就很麻烦了。举个例子，假如你想读硬盘上的一个文件，因为操作系统有这样的功能，所以就不必在自己的程序中再写一套代码，直接调用操作系统例程就可以了。

但是操作系统通常不会给出或者公布硬盘读写例程的段地址和偏移地址，因为操作系统也是经常修改的，经常发布新的版本。这样一来，例程的入口地址也会跟着变化。而且，也不能保证每次启动计算机之后，操作系统总待在同一个内存位置。

因为有了软中断，每次操作系统加载完自己之后，以中断处理程序的形式提供硬盘读写功能，并把该例程的地址填写到中断向量表中。这样，无论在什么时候，用户程序需要该功能时，直接发出一个软中断即可，不需要知道具体的地址。

最有名的软中断是BIOS中断，之所以称为BIOS中断，是因为这些中断功能是在计算机加电之后，BIOS程序执行期间建立起来的。换句话说，这些中断功能在加载和执行主引导扇区之前，就已经可以使用了。

BIOS中断，又称为功能调用，主要是为了方便使用最基本的硬件访问功能。不同的硬件使用不同的中断号，比如，使用键盘服务时，中断号是0x16，即

```c
int 0x16
```

通常，为了区分针对同一硬件的不同功能，使用寄存器AH来指定具体的功能编号。举例来说，以下指令用于从键盘读取一个按键：

```c
mov ah, 0x00		;从键盘读字符
int 0x16			;键盘服务。返回时，字符代码在寄存器AL中
```

在这里，寄存器AH的内容是0x00时，执行int 0x16后，中断服务例程会监视键盘动作，当他返回时，会在寄存器AL中存放按键的ASCII码。

![image-20220718113102987](G:\images\Linux\image-20220718113102987.png)

```assembly
         ;代码清单9-2
         ;文件名：c09_2.asm
         ;文件说明：用于演示BIOS中断的用户程序 
         ;创建日期：2012-3-28 20:35
         
;===============================================================================
SECTION header vstart=0                     ;定义用户程序头部段 
    program_length  dd program_end          ;程序总长度[0x00]
    
    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code.start   ;段地址[0x06] 
    
    realloc_tbl_len dw (header_end-realloc_begin)/4
                                            ;段重定位表项个数[0x0a]
    
    realloc_begin:
    ;段重定位表           
    code_segment    dd section.code.start   ;[0x0c]
    data_segment    dd section.data.start   ;[0x14]
    stack_segment   dd section.stack.start  ;[0x1c]
    
header_end:                
    
;===============================================================================
SECTION code align=16 vstart=0           ;定义代码段（16字节对齐） 
start:
      mov ax,[stack_segment]
      mov ss,ax
      mov sp,ss_pointer
      mov ax,[data_segment]
      mov ds,ax
      
      mov cx,msg_end-message
      mov bx,message
      
 .putc:
      mov ah,0x0e		;功能编号
      mov al,[bx]		;显示字符的ascii码
      int 0x10			;引发BIOS中断显示字符
      inc bx
      loop .putc

 .reps:
      mov ah,0x00		;使用软中断从键盘获取一个字符的ascii保存在al
      int 0x16
      
      mov ah,0x0e		;使用软中断显示一个字符，该字符在寄存器al中
      mov bl,0x07
      int 0x10

      jmp .reps

;===============================================================================
SECTION data align=16 vstart=0

    message       db 'Hello, friend!',0x0d,0x0a
                  db 'This simple procedure used to demonstrate '
                  db 'the BIOS interrupt.',0x0d,0x0a
                  db 'Please press the keys on the keyboard ->'
    msg_end:
                   
;===============================================================================
SECTION stack align=16 vstart=0
           
                 resb 256
ss_pointer:
 
;===============================================================================
SECTION program_trail
program_end:
```

